apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'eclipse-wtp'

group = 'v.a.org.springframework.session.aerospike'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    jcenter()
}

// Integration test setup
configurations {
	integrationTestCompile {
		extendsFrom testCompile
	}
	integrationTestRuntime {
		extendsFrom integrationTestCompile, testRuntime
	}
}

subprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

sourceSets {
	integrationTest {
		java.srcDir file('src/integration-test/java')
		groovy.srcDirs file('src/integration-test/groovy')
		resources.srcDir file('src/integration-test/resources')
		compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.integrationTestCompile
		runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntime
	}
}

task integrationTest(type: Test, dependsOn: jar) {
	testClassesDir = sourceSets.integrationTest.output.classesDir
	logging.captureStandardOutput(LogLevel.INFO)
	classpath = sourceSets.integrationTest.runtimeClasspath
	maxParallelForks = 1
	reports {
		html.destination = project.file("$project.buildDir/reports/integration-tests/")
		junitXml.destination = project.file("$project.buildDir/integration-test-results/")
	}
}

eclipse {
	classpath {
		plusConfigurations += [ configurations.integrationTestCompile ]
	}
}

project.idea.module {
	scopes.TEST.plus += [project.configurations.integrationTestRuntime]
}